pragma ComponentBehavior: Bound

import QtQuick
import Quickshell.Hyprland

Item {
  id: root

  required property string widgetId
  required property var config
  required property string edge
  required property var barWindow
  required property var theme
  required property var systemState
  required property var panelHost

  implicitWidth: workspaceRow.implicitWidth
  implicitHeight: workspaceRow.implicitHeight

  component WorkspaceButton: Item {
    id: workspaceButton

    required property var workspace

    readonly property bool configured: root.config.alwaysShow.indexOf(workspace.id) >= 0
    readonly property bool shown: workspace.id > 0 && (configured
      || (root.config.includeFocused && workspace.focused)
      || (root.config.includeOccupied && workspace.toplevels.values.length > 0))

    visible: shown
    implicitWidth: shown ? content.implicitWidth : 0
    implicitHeight: shown ? content.implicitHeight : 0
    height: root.height

    Row {
      id: content
      anchors.centerIn: parent
      opacity: workspaceButton.workspace.toplevels.values.length > 0 || workspaceButton.workspace.focused ? 1 : 0.55

      Text {
        text: "["
        color: root.theme.palette.accent
        opacity: workspaceButton.workspace.focused ? 1 : 0
        font.family: root.theme.typography.monoFamily
        font.pixelSize: root.theme.typography.readoutSize
        font.weight: root.theme.typography.weight
        font.styleName: root.theme.typography.style
      }

      Text {
        text: workspaceButton.workspace.id
        color: workspaceButton.workspace.focused ? root.theme.palette.accent : root.theme.palette.foreground
        font.family: root.theme.typography.monoFamily
        font.pixelSize: root.theme.typography.readoutSize
        font.weight: root.theme.typography.weight
        font.styleName: root.theme.typography.style
      }

      Text {
        text: "]"
        color: root.theme.palette.accent
        opacity: workspaceButton.workspace.focused ? 1 : 0
        font.family: root.theme.typography.monoFamily
        font.pixelSize: root.theme.typography.readoutSize
        font.weight: root.theme.typography.weight
        font.styleName: root.theme.typography.style
      }
    }

    MouseArea {
      anchors.fill: parent
      cursorShape: Qt.PointingHandCursor
      // Hyprland's Lua config takes dispatch arguments as Lua expressions
      onClicked: Hyprland.dispatch(`hl.dsp.focus({ workspace = ${workspaceButton.workspace.id} })`)
    }
  }

  Row {
    id: workspaceRow
    x: (parent.width - width) / 2
    width: implicitWidth
    height: parent.height

    Repeater {
      model: Hyprland.workspaces
      WorkspaceButton {
        required property var modelData
        workspace: modelData
      }
    }
  }
}
